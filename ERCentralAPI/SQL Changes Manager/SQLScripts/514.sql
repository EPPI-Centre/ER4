USE [Reviewer]
GO

/****** Object:  StoredProcedure [dbo].[st_ReviewSetMove]    Script Date: 18/10/2022 15:21:15 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE or ALTER procedure [dbo].[st_ReviewSetMove]
(
	@REVIEW_SET_ID BIGINT,
	@NEW_SET_ORDER INT
)

As

SET NOCOUNT ON

DECLARE @OLD_SET_ORDER bigint
DECLARE @SET_ID BIGINT
DECLARE @REVIEW_ID int

SELECT @OLD_SET_ORDER = SET_ORDER, @SET_ID = SET_ID, @REVIEW_ID = REVIEW_ID
FROM TB_REVIEW_SET
WHERE REVIEW_SET_ID = @REVIEW_SET_ID

if @NEW_SET_ORDER < @OLD_SET_ORDER -- moving up
begin
	-- move existing to make room
	UPDATE TB_REVIEW_SET
		SET SET_ORDER = SET_ORDER + 1
		WHERE REVIEW_ID = @REVIEW_ID
		AND SET_ORDER >= @NEW_SET_ORDER
		AND SET_ORDER < @OLD_SET_ORDER

	-- change target
	UPDATE TB_REVIEW_SET
		SET SET_ORDER = @NEW_SET_ORDER
		WHERE REVIEW_ID = @REVIEW_ID
		and REVIEW_SET_ID = @REVIEW_SET_ID

end
else  -- moving down
begin

	-- move existing to make room
	UPDATE TB_REVIEW_SET
		SET SET_ORDER = SET_ORDER - 1
		WHERE REVIEW_ID = @REVIEW_ID
		AND SET_ORDER > @OLD_SET_ORDER
		AND SET_ORDER <= @NEW_SET_ORDER

	-- change target
	UPDATE TB_REVIEW_SET
		SET SET_ORDER = @NEW_SET_ORDER
		WHERE REVIEW_ID = @REVIEW_ID
		and REVIEW_SET_ID = @REVIEW_SET_ID

end

SET NOCOUNT OFF

GO

