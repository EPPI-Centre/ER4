USE [Reviewer]
GO
/****** Object:  StoredProcedure [dbo].[st_ReviewStatisticsCodeSetsComplete]    Script Date: 3/15/2022 4:54:26 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER procedure [dbo].[st_ReviewStatisticsCodeSetsComplete]
(
	@REVIEW_ID INT
)
with recompile
As

SET NOCOUNT ON

SELECT SET_NAME, TB_ITEM_SET.SET_ID, COUNT(DISTINCT TB_ITEM_SET.ITEM_ID) AS TOTAL
FROM TB_ITEM_SET
INNER JOIN TB_REVIEW_SET ON TB_REVIEW_SET.SET_ID = TB_ITEM_SET.SET_ID
INNER JOIN TB_ITEM_REVIEW ON TB_ITEM_REVIEW.REVIEW_ID = TB_REVIEW_SET.REVIEW_ID AND TB_ITEM_REVIEW.ITEM_ID = TB_ITEM_SET.ITEM_ID
INNER JOIN TB_SET ON TB_SET.SET_ID = TB_ITEM_SET.SET_ID
WHERE TB_ITEM_REVIEW.REVIEW_ID = @REVIEW_ID AND TB_ITEM_SET.IS_COMPLETED = 'TRUE' AND IS_DELETED = 'FALSE'
GROUP BY TB_ITEM_SET.SET_ID, SET_NAME

SET NOCOUNT OFF
GO


USE [Reviewer]
GO
/****** Object:  StoredProcedure [dbo].[st_ReviewStatisticsCodeSetsReviewersComplete]    Script Date: 3/15/2022 5:00:42 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER procedure [dbo].[st_ReviewStatisticsCodeSetsReviewersComplete]
(
	@REVIEW_ID INT,
	@SET_ID INT
)
with recompile
As

SET NOCOUNT ON

SELECT SET_NAME, TB_ITEM_SET.SET_ID, TB_ITEM_SET.CONTACT_ID, CONTACT_NAME, COUNT(DISTINCT TB_ITEM_SET.ITEM_ID) AS TOTAL
FROM TB_ITEM_SET
INNER JOIN TB_REVIEW_SET ON TB_REVIEW_SET.SET_ID = TB_ITEM_SET.SET_ID
INNER JOIN TB_ITEM_REVIEW ON TB_ITEM_REVIEW.REVIEW_ID = TB_REVIEW_SET.REVIEW_ID AND TB_ITEM_REVIEW.ITEM_ID = TB_ITEM_SET.ITEM_ID
INNER JOIN TB_SET ON TB_SET.SET_ID = TB_ITEM_SET.SET_ID
INNER JOIN TB_CONTACT ON TB_CONTACT.CONTACT_ID = TB_ITEM_SET.CONTACT_ID
WHERE TB_ITEM_REVIEW.REVIEW_ID = @REVIEW_ID AND TB_ITEM_SET.IS_COMPLETED = 'TRUE' and IS_DELETED = 'FALSE'
	AND TB_ITEM_SET.SET_ID = @SET_ID
GROUP BY TB_ITEM_SET.SET_ID, SET_NAME, TB_ITEM_SET.CONTACT_ID, CONTACT_NAME

SET NOCOUNT OFF
GO

USE [Reviewer]
GO
/****** Object:  StoredProcedure [dbo].[st_ReviewStatisticsCodeSetsReviewersIncomplete]    Script Date: 3/15/2022 5:01:20 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER procedure [dbo].[st_ReviewStatisticsCodeSetsReviewersIncomplete]
(
	@REVIEW_ID INT,
	@SET_ID INT 
)
with recompile
As

SET NOCOUNT ON
--the local variables here stop all sniffing possibilities, according to:
--https://stackoverflow.com/questions/440944/sql-server-query-fast-but-slow-from-procedure
--and: http://www.sqlpointers.com/2006/11/parameter-sniffing-stored-procedures.html
DECLARE @rid INT
set @rid = @REVIEW_ID
DECLARE @sid INT 
SET @sid = @SET_ID

declare @t table (ItemId bigint primary key)
declare @unt table (ItemId bigint primary key)
insert into @t SELECT distinct IS2.ITEM_ID
	FROM TB_ITEM_SET IS2
	INNER JOIN TB_ITEM_REVIEW IR2 ON IR2.ITEM_ID = IS2.ITEM_ID AND IR2.REVIEW_ID = @rid
	WHERE IS2.IS_COMPLETED = 'TRUE' AND IS2.SET_ID = @sid AND IR2.IS_DELETED = 'FALSE'
	--option (optimize for unknown)
--select @@ROWCOUNT

insert into @unt SELECT distinct IS2.ITEM_ID
	FROM TB_ITEM_SET IS2
	INNER JOIN TB_ITEM_REVIEW IR2 ON IR2.ITEM_ID = IS2.ITEM_ID AND IR2.REVIEW_ID = @rid
	WHERE IS2.IS_COMPLETED = 'FALSE' AND IS2.SET_ID = @sid AND IR2.IS_DELETED = 'FALSE'
	AND NOT IR2.ITEM_ID IN
	(
		select ItemId from @t
	)
	--option (optimize for unknown)
--select @@ROWCOUNT

SELECT SET_NAME, IS1.SET_ID, IS1.CONTACT_ID, CONTACT_NAME, COUNT(DISTINCT IS1.ITEM_ID) AS TOTAL
FROM @unt un Inner Join TB_ITEM_SET IS1 on IS1.ITEM_ID = un.ItemId and IS1.SET_ID = @sid
INNER JOIN TB_REVIEW_SET ON TB_REVIEW_SET.SET_ID = IS1.SET_ID
INNER JOIN TB_ITEM_REVIEW ON TB_ITEM_REVIEW.REVIEW_ID = TB_REVIEW_SET.REVIEW_ID AND TB_ITEM_REVIEW.ITEM_ID = IS1.ITEM_ID
INNER JOIN TB_SET ON TB_SET.SET_ID = IS1.SET_ID
INNER JOIN TB_CONTACT ON TB_CONTACT.CONTACT_ID = IS1.CONTACT_ID
WHERE TB_ITEM_REVIEW.REVIEW_ID = @rid AND IS1.IS_COMPLETED = 'FALSE' 
--AND TB_ITEM_REVIEW.IS_DELETED = 'FALSE' 
GROUP BY IS1.SET_ID, SET_NAME, IS1.CONTACT_ID, CONTACT_NAME
--option (optimize for unknown)

SET NOCOUNT OFF
GO